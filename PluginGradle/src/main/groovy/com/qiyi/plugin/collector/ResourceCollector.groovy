package com.qiyi.plugin.collector


import com.android.build.gradle.api.ApkVariant
import com.android.build.gradle.tasks.ProcessAndroidResources
import com.android.builder.dependency.MavenCoordinatesImpl
import com.android.builder.dependency.level2.AndroidDependency
import com.android.builder.model.MavenCoordinates
import com.android.utils.FileUtils
import com.qiyi.plugin.collector.dependence.AarDependenceInfo
import com.qiyi.plugin.collector.dependence.DependenceInfo

import com.qiyi.plugin.collector.res.ResourceEntry
import com.qiyi.plugin.collector.res.StyleableEntry
import com.google.common.collect.ArrayListMultimap
import com.google.common.collect.ListMultimap
import com.google.common.collect.Lists
import com.qiyi.plugin.QYPluginExtension
import org.gradle.api.Project
import org.gradle.api.artifacts.Configuration
import org.gradle.api.artifacts.ResolvedDependency

/**
 * Collect all(host+plugin) resources&styleables in the APK and reassign the resource ID
 *
 * @author zhengtao
 */
class ResourceCollector {

    private Project project
    private QYPluginExtension pluginExt
    private ApkVariant apkVariant
    /**
     * Gradle task of process resource in Android build system
     */
    private ProcessAndroidResources processResTask
    /**
     * R symbol File that records all resources, generated by aapt
     */
    private File allRSymbolFile
    /**
     * Host R symbol File, need saved after the host APK compilation
     */
    private File hostRSymbolFile

    /**
     * Map of all resources, KEY is the resource type, e.g. drawable, VALUE is all entries of this type
     */
    ListMultimap<String, ResourceEntry> allResources = ArrayListMultimap.create()
    /**
     * List of all styleables
     */
    List<StyleableEntry> allStyleables = Lists.newArrayList()


    private ListMultimap<String, ResourceEntry> hostResources = ArrayListMultimap.create()
    private List<StyleableEntry> hostStyleables = Lists.newArrayList()

    /**
     * pluginResources = allResources - hostResources
     */
    ListMultimap<String, ResourceEntry> pluginResources = ArrayListMultimap.create()
    List<StyleableEntry> pluginStyleables = Lists.newArrayList()

    //group:artifact:version
    Set<String> hostDependencies = [] as Set<String>

    Set<AarDependenceInfo> retainedAarLibs = [] as Set<AarDependenceInfo>
    Set<DependenceInfo> stripDependencies = [] as Collection<DependenceInfo>

    public ResourceCollector(Project project, ProcessAndroidResources par, ApkVariant variant) {

        this.project = project
        this.pluginExt = project.extensions.findByType(QYPluginExtension)

        this.apkVariant = variant
        processResTask = par

        allRSymbolFile = pluginExt.isHigherAGP ? par.textSymbolOutputFile : new File(par.textSymbolOutputDir, 'R.txt')
        hostRSymbolFile = pluginExt.hostSymbolFile
    }

    /**
     * Perform resource collection and ID redistribution
     */
    def collect() {
        //1. First, resolve dependencies info from prepareDependenciesTask
        prepareDependencies()

        //2、First, collect all resources by parsing the R symbol file.
        parseResEntries(allRSymbolFile, allResources, allStyleables)

        //3、Then, collect host resources by parsing the host apk R symbol file, should be stripped.
        parseResEntries(hostRSymbolFile, hostResources, hostStyleables)
        stripDependencies.each {
            if (it instanceof AarDependenceInfo) {
                parseResEntries(it.symbolFile, hostResources, hostStyleables)
            }
        }

        //4、Compute the resources that should be retained in the plugin apk.
        filterPluginResources()

        //5、Reassign the resource ID. If the resource entry exists in host apk, the reassign ID
        //   should be same with value in host apk; If the resource entry is owned by plugin project,
        //   then we should recalculate the ID value.
        reassignPluginResourceId()

        //6、Collect all the resources in the retained AARs, to regenerate the R java file that uses the new resource ID
        retainedAarLibs.each {
            gatherReservedAarResources(it)
        }
    }

    /**
     * Collect project dependencies from host or plugin self
     */
    private void prepareDependencies() {

        if (pluginExt.hostDependencies != null) {
            pluginExt.hostDependencies.split(";").each { dep ->
                def module = dep.split(':')
                hostDependencies.add("${module[0]}:${module[1]}")
            }
        }

        Set<AndroidDependency> androidDependencies = [] as Set<AndroidDependency>
         if (pluginExt.isHigherAGP) {
            // TODO read Android Gradle Plugin 3.0.0 source code to find solution
            DependencyCollector dependencyCollector = new DependencyCollector(project, apkVariant)
            androidDependencies = dependencyCollector.androidDependencies
//            Configuration configuration = project.configurations.getByName("${apkVariant.name}CompileClasspath")
//            if (configuration != null) {
//                configuration.resolvedConfiguration.firstLevelModuleDependencies.each {
//                    collectRetainedAars(it, androidDependencies)
//                }
//            }
        } else {
            androidDependencies = processResTask.androidDependencies
        }

        androidDependencies.each {
            println "${it.extractedFolder}, ${it.symbolFile}, ${it.jarFile}"

            def mavenCoordinates = it.coordinates as MavenCoordinates
            if (hostDependencies.contains("${mavenCoordinates.groupId}:${mavenCoordinates.artifactId}")) {
                println "strip Aar: ${mavenCoordinates.groupId}:${mavenCoordinates.artifactId}:${mavenCoordinates.version}"
                AarDependenceInfo aarDependenceInfo = new AarDependenceInfo(
                        mavenCoordinates.groupId,
                        mavenCoordinates.artifactId,
                        mavenCoordinates.version,
                        it)
                aarDependenceInfo = aarDependenceInfo.fixAarManifest(project, apkVariant)
                aarDependenceInfo = aarDependenceInfo.fixRSymbol(project, apkVariant)
                stripDependencies.add(aarDependenceInfo)
            } else {
                println "retained Aar: ${mavenCoordinates.groupId}:${mavenCoordinates.artifactId}:${mavenCoordinates.version}"
                AarDependenceInfo aarDependenceInfo = new AarDependenceInfo(
                        mavenCoordinates.groupId,
                        mavenCoordinates.artifactId,
                        mavenCoordinates.version,
                        it)
                aarDependenceInfo = aarDependenceInfo.fixAarManifest(project, apkVariant)
                aarDependenceInfo = aarDependenceInfo.fixRSymbol(project, apkVariant)
                retainedAarLibs.add(aarDependenceInfo)
            }
        }
    }


    /** Collect the dependency aars */
    protected void collectRetainedAars(ResolvedDependency node, Set<AndroidDependency> outAndroidDependencies) {
        def group = node.moduleGroup
        def name = node.moduleName
        def version = node.moduleVersion


        println "collectRetainedAars dependency ${group}:${name}:${version}, dependency name:${node.getClass().getName()}"

        node.allModuleArtifacts.each {
            println "artifact: ${it.name}, file: ${it.file.absolutePath}"
            if (it.extension == "aar" && version == it.getModuleVersion().getId().version) {
                MavenCoordinatesImpl mavenCoordinates = new MavenCoordinatesImpl(
                        it.getModuleVersion().getId().getGroup(),
                        it.getModuleVersion().getId().getName(),
                        it.getModuleVersion().getId().getVersion(),
                        it.getExtension(),
                        it.getClassifier()
                )

                final String variantName = it.getClassifier()
                boolean  isSubProject = false
                Project subProject = project.findProject(name)
                if (subProject != null) {
                    isSubProject = subProject.plugins.hasPlugin("com.android.library")
                }
                if (isSubProject) {
                    File stagingDir = FileUtils.join(
                            subProject.buildDir,
                            "intermediates",
                            "bundles",
                            variantName != null ? variantName : "default"
                    )
                    AndroidDependency androidDependency = new AndroidDependency(
                            it.file,
                            mavenCoordinates,
                            name,
                            subProject.path,
                            stagingDir,
                            stagingDir,
                            variantName,
                            true
                    )
                    outAndroidDependencies.add(androidDependency)
                } else {
                    // jars/classes.jar
                    // res/xxx
                    File explodedDir = it.file.getParentFile().getParentFile()
                    AndroidDependency androidDependency = AndroidDependency.createExplodedAarLibrary(
                            it.file,
                            mavenCoordinates,
                            name,
                            null,
                            explodedDir)
                    outAndroidDependencies.add(androidDependency)
                }
            }
        }

        node.children.each {
            collectRetainedAars(it, outAndroidDependencies)
        }
    }

    /**
     * Collect resources and styleables by parsing R symbol file
     * @param RSymbolFile R symbol file records the resource entries
     * @param resourcesMap Map used to store resources
     * @param styleableList List used to store styleables
     */
    private static void parseResEntries(File RSymbolFile, ListMultimap resourcesMap, List styleableList) {
        if (RSymbolFile == null || !RSymbolFile.exists()) {
            return
        }
        RSymbolFile.eachLine { line ->
            /**
             *  Line Content:
             *  Common Res:  int string abc_action_bar_home_description 0x7f090000
             *  Styleable:   int[] styleable TagLayout { 0x010100af, 0x7f0102b5, 0x7f0102b6 }
             *            or int styleable TagLayout_android_gravity 0
             */
            if (!line.empty) {
                def tokenizer = new StringTokenizer(line)
                def valueType = tokenizer.nextToken()     // value type (int or int[])
                def resType = tokenizer.nextToken()      // resource type (attr/string/color etc.)
                def resName = tokenizer.nextToken()
                def resId = tokenizer.nextToken('\r\n').trim()

                if (resType == 'styleable') {
                    styleableList.add(new StyleableEntry(resName, resId, valueType))
                } else {
                    resourcesMap.put(resType, new ResourceEntry(resType, resName, Integer.decode(resId)))
                }
            }
        }
    }

    /**
     * Filter out the resources that need to be retained in the plugin apk,
     * pluginResources = allResources - hostResources
     */
    private void filterPluginResources() {
        allResources.values().each {
            def index = hostResources.get(it.resourceType).indexOf(it)
            if(index >= 0){
                /**
                 * If the resource entry exists in host apk, assign the host resource ID of this entry
                 * as the new resource id.
                 * Then replace the object reference in host resource collection with the object
                 * in all resource collection, to make both of them point to the same object
                 */
                it.newResourceId = hostResources.get(it.resourceType).get(index).resourceId
                hostResources.get(it.resourceType).set(index, it)
            } else {
                pluginResources.put(it.resourceType, it)
            }
        }

        allStyleables.each {
            def index = hostStyleables.indexOf(it)
            if(index >= 0) {
                /**
                 * Do not support the same name but different content styleable entry
                 */
                it.value = hostStyleables.get(index).value
                hostStyleables.set(index, it)
            } else {
                pluginStyleables.add(it)
            }
        }
    }

    /**
     * Reassign the ID for resources need retained in the plugin apk
     * Set the packageId specified in the build.gradle file, and reassign type&entry ID
     */
    private void reassignPluginResourceId() {
        //The value of aar type should be 1
        def attrTypeId = 1
        //Other types  are allocated from 2
        def lastTypeId = 2
        pluginResources.keySet().each { String resType ->
            def typeId = 0
            def entryId = 0
            if (resType == 'attr') {
                typeId = attrTypeId
            } else {
                typeId = lastTypeId++
            }
            pluginResources.get(resType).each {
                it.setNewResourceId(pluginExt.packageId, typeId, entryId++)
            }
        }

        List<ResourceEntry> attrEntries = allResources.get('attr')

        pluginStyleables.findAll { it.valueType == 'int[]'}.each { StyleableEntry styleableEntry->
            List<String> values = styleableEntry.valueAsList
            values.eachWithIndex { hexResId, idx ->
                ResourceEntry resEntry = attrEntries.find { it.hexResourceId == hexResId }
                values[idx] = resEntry?.hexNewResourceId
            }
            styleableEntry.value = values
        }
    }

    /**
     * Collect all resources the aar project can access
     * @param aarDependenceInfo aar dependence info
     */
    def gatherReservedAarResources(AarDependenceInfo aarDependenceInfo) {
        def aarResKeys = aarDependenceInfo.resourceKeys
        if (aarResKeys.empty) return

        allResources.keySet().each { resType ->
            allResources.get(resType).each { resEntry ->
                if (aarResKeys.contains("${resType}:${resEntry.resourceName}")) {
                    aarDependenceInfo.aarResources.put(resType, resEntry)
                }
            }
        }

        aarDependenceInfo.aarStyleables = allStyleables.findAll { styleableEntry ->
            aarResKeys.contains("styleable:${styleableEntry.name}")
        }
    }

    /**
     * Returns the mapping from the original ID to the redistributed ID
     */
    def getResIdMap() {
        def idMap = [:] as Map<Integer, Integer>
        allResources.values().each { resEntry ->
            idMap.put(resEntry.resourceId, resEntry.newResourceId)
        }
        return idMap
    }


    public void dump() {
        final def resSplitDir = new File(project.buildDir, 'generated')

        final def retainTypeFile = new File(resSplitDir, 'retainType.txt')
        if (!retainTypeFile.exists()) {
            retainTypeFile.createNewFile()
        }
        retainTypeFile.withPrintWriter { pw ->
            pluginResources.values().each {
                pw.println "${it.resourceType} ${it.resourceName} 0x${Integer.toHexString(it.resourceId)} 0x${Integer.toHexString(it.newResourceId)}"
            }
            pw.println "****************Styleables*****************"
            pluginStyleables.each {
                pw.println "${it.name} ${it.valueType} ${it.value}"
            }
        }


        final def allTypeFile = new File(resSplitDir, "allType.txt")
        if (!allTypeFile.exists()) {
            allTypeFile.createNewFile()
        }
        allTypeFile.withPrintWriter { pw ->
            allResources.values().each {
                pw.println "${it.resourceType} ${it.resourceName} 0x${Integer.toHexString(it.resourceId)} 0x${Integer.toHexString(it.newResourceId)}"
            }
            pw.println "****************Styleables*****************"
            allStyleables.each {
                pw.println "${it.name} ${it.valueType} ${it.value}"
            }
        }

        final def vendorTypeFile = new File(resSplitDir, 'vendorType.txt')
        if (!vendorTypeFile.exists()) {
            vendorTypeFile.createNewFile()
        }

        vendorTypeFile.withPrintWriter { pw ->
            retainedAarLibs.each { aarLib ->
                pw.println "${aarLib.name}"

                aarLib.aarResources.values().each {
                    pw.println "${it.resourceType} ${it.resourceName} 0x${Integer.toHexString(it.resourceId)} 0x${Integer.toHexString(it.newResourceId)}"
                }
            }
        }
    }

}